***(
    Infinite states
    --------------

    EXample in Figure 3b (infinite number of states)
    The location BAD is not reachable and the search command loops (see 
    the bounded limit in the smt-search command below)

*)
load pta-base .

mod MODEL is
    inc MODEL-BASE .
    pr REAL .

    --- ---------------------------
    vars X Y      : Real .
    vars X' Y'    : Real .
    vars T      : Real .
    --- ---------------------------

    --- Configurations
    ops l0 l1 bad : -> Location .
    op <_:_>  : Location Real   -> NState .
    op [_:_]  : Location Real   -> DState .

    crl [l0-l1] : 
        < l0 : X > =>
        [ l1 : X  ] 
        if ( 5/1 <= X and 10/1 >= X ) = true .

    crl [l0-tick] : 
        [ l0 : X  ] =>
        < l0 : X + T  > 
        if (T >= 0/1) = true [nonexec] .

    crl [l1-l0] : 
        < l1 : X    > =>
        [ l0 : 0/1  ] 
        if (true) = true .

    crl [l1-tick] : 
        [ l1 : X  ] =>
        < l1 : X + T  > 
        if (T >= 0/1 and 10/1 >= X + T) = true [nonexec] .

    crl [l1-bad] : 
        < l1 :  X  > =>
        [ bad : X  ] 
        if (X >= 11/1) = true .

    crl [bad-tick] : 
        [ bad : X ] =>
        < bad : X + T > 
        if (T >= 0/1) = true [nonexec] .
endm